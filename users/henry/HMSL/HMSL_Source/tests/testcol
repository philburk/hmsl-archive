\ Debug Collection Message Passing.

ANEW TASK-TESTCOL

\ The following word will need to be modified to select
\ the MIDI channels you want to play on in your studio.
: SITE-RANGE ( -- lo hi , site specific range of available channels)
    1 8
;

OB.COLLECTION COLL-TEST1
ACT.SEQUENTIAL: COLL-TEST1

OB.COLLECTION COLL-TEST2
ACT.SEQUENTIAL: COLL-TEST2

\ Functions to execute at various times in the life
\ of the collection or player.

: SHOW.START  ." Start " name: [] cr ;
: SHOW.REPEAT  ." Repeat " name: [] cr ;
: SHOW.STOP  ." Stop " name: [] cr ;

\ Fill shapes with "melodies".
: DC.FILL.SHAPES
\ Make them big enough to add to in Shape Editor.
    16 3 new: shape-1
    20 20 60 add: shape-1
    20 30 70 add: shape-1
    20 25 80 add: shape-1
    20 35 90 add: shape-1
\
    16 3 new: shape-2
    30 24 100 add: shape-2
    30 29 100 add: shape-2
    30 12 100 add: shape-2
    30 13 80  add: shape-2
    30 22 100 add: shape-2
    30 22 80  add: shape-2
\
;

\ Build hierarchy of morphs.
: DC.INIT
    dc.fill.shapes
    site-range put.channel.range: ins-midi-1
    ins-midi-1 put.instrument: player-1
    site-range put.channel.range: ins-midi-2
    ins-midi-2 put.instrument: player-2

    0 shape-1 0stuff: player-1
    1 put.repeat: player-1
\
    0 shape-2 0stuff: player-2
    1 put.repeat: player-2
    40 put.delay: player-2
\
\ Make available for Shape Editor.
    clear: shape-holder
    shape-1 add: shape-holder
    shape-2 add: shape-holder
\
\ Load special functions.
    'c show.start  put.start.function: player-2
    'c show.start  put.start.function: coll-test2
    'c show.repeat put.repeat.function: coll-test2
    'c show.stop   put.stop.function: coll-test2
\
\ Put players in seq collection.
    0 player-1 player-2 0stuff: coll-test1
    0 coll-test1 0stuff: coll-test2
    4 put.repeat: coll-test2
\
    ." Hierarchy of piece!" cr
    print.hierarchy: coll-test2 cr
    ." Hit any key to continue." key drop
;

: DC.PLAY  ( -- , Play the collection.)
    coll-test2 hmsl.play
;

: DC.TERM  ( -- , Reset & Free allocated data. )
    default.hierarchy: coll-test2
    free.hierarchy: coll-test2
;

: TESTCOL  ( -- , Do whole thing. )
    dc.init  dc.play  dc.term
;
." Enter:    TESTCOL     for collection demo." cr
