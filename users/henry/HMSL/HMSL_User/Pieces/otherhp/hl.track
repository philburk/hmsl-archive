\ TRACKER
\
\ Play the last 16 notes received from a MIDI keyboard
\ in a repeating pattern.  Use a circular buffer
\ to hold the notes.
\ jhhl: added ability to time the notes.
\
\ Author: Phil Burk
\ Copyright 1986
\ All Rights Reserved
\
\ MOD: PLB 6/4/87 Use MIDI.PARSER.ON

ANEW TASK-TRACKER

VARIABLE MT-SHAPE-MAX
VARIABLE MT-NOTE-DUR
VARIABLE MT-NEXT-INDEX
\ "real time" to create "durations"
variable mt-Last-time
variable mt-last-note
variable mt-last-vel

variable mt-this-time
variable mt-this-note
variable mt-this-vel
variable mt-tix-lim
variable mt-speed

100 mt-speed ! \ snazzy percentage tweeker for time porpoises
128 mt-shape-max !
8 mt-note-dur !
120 Mt-Tix-Lim !
\ longer than 1 sec ? cut in immediately.
: tix
mt-this-time @ mt-last-time @ - mt-speed @ 100 */ 
dup Mt-Tix-Lim @ > if drop 1 clear: shape-1 mt-next-index off then ;

\ This word gets called when a note on is recieved.
\ aso keeps track of "spaces" via the vel=0 trick.
: MT.NOTE.ON ( note velocity -- , bounce note )
 rtc.Time@ mt-This-time !
mt-this-vel ! mt-this-note !

\ ." Tx:" tix . mt-this-note ? mt-this-vel ?  cr
  many: shape-1 max.elements: shape-1  >=
        IF  ( Put in next position in shape. )
             Tix mt-last-note @ mt-last-vel @
            mt-next-index @ 1+ dup mt-next-index !
            max.elements: shape-1 mod
            put: shape-1
        ELSE
           Tix mt-last-note @ mt-last-vel @ add: shape-1
        THEN
    mt-This-time @ mt-last-time !
    mt-this-note @ mt-last-note !
    mt-this-vel @ mt-last-vel ! 
; 
\ noff = a note on w/ 0 vel. 
: mt.note.off
drop 0 mt.note.on ;

: MT.BUILD.PLAYER ( -- , Setup player with shape. )
    mt-shape-max @ 3 new: shape-1
    mt-note-dur @ 0 0 add: shape-1 ( start with a rest )
\
    0 shape-1 0stuff: player-1
    4000 put.repeat: player-1
\
    0 put.gamut: ins-midi-1
    0 put.offset: ins-midi-1
    ins-midi-1 put.instrument: player-1
;

: MT.INIT  ( -- , Initialize Piece )
    mt.build.player
\
\ Set parser vector so that whenever a NOTE ON is recieved
\ the word MT.NOTE.ON will be called.  This will occur
\ when MIDI.PARSE is polled (called).
    mp.reset
    'c mt.note.on  mp-on-vector !
    'c mt.note.off  mp-off-vector !
    'c midi.preset   mp-program-vector !
\
\ Make shapes available.
    clear: shape-holder
    shape-1 add: shape-holder
rtc.time@ mt-last-time !

;

: MT.PLAY ( -- )
    midi.clear
    midi.parser.on
    player-1 hmsl.play  ( no preposted morphs )
    midi.parser.off
;

: MT.TERM ( -- , Clean up for others. )
    default.hierarchy: player-1
    free.hierarchy: player-1
    mp.reset       ( reset midi parser vectors )
;

: TRACKER ( -- )
    mt.init  mt.play  mt.term
;

." Enter:   TRACKER  to play this piece." cr
